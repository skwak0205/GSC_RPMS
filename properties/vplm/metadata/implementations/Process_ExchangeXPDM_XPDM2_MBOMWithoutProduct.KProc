/*********************************************************************************************************************************/
/* Exchange XPDM procedure for MBOM                                                                                        */
/* --> Retrieves all agregated PLM objects (References, Ref Instances, Rep References, Rep Instances, Ports and Connections)     */
/*********************************************************************************************************************************/
#Prog Process_ExchangeXPDM_XPDM2_MBOMWithoutProduct( iPLMIDSet : #In PLMIDSet, oPLMIDSet : #Out PLMIDSet ) : VoidType
{
	let PLMRouteSetCapableRsc (PLMRouteSet)
	let PLMRouteSetProductSTL (PLMRouteSet)
	let PLMRouteSetResourceStructure (PLMRouteSet)
	let PLMRouteSetSystemScope (PLMRouteSet)
	let PLMRouteSetRFLPCnx (PLMRouteSet)
	let PLMRouteSetImplementingOp (PLMRouteSet)
	let PLMRouteSetPortsAndCnx (PLMRouteSet)
	let PLMRouteSetAlternate (PLMRouteSet)
	let PLMRouteSetReq (PLMRouteSet)
	let PLMRouteSetSubstitute (PLMRouteSet)
	let PLMRouteSetResponsibility (PLMRouteSet)
	let PLMRouteSetHistoRef (PLMRouteSet)
	let PLMRouteSetProductAggrRep (PLMRouteSet)


	let PLMIDSetProcessRef (PLMIDSet)
	let PLMIDSetProcessInst (PLMIDSet)
	let PLMIDSetProcess (PLMIDSet)
	let PLMIDSetPrecedencePorts (PLMIDSet)
	let PLMIDSetPrecedenceCnx (PLMIDSet)
	let PLMIDSetValidPrecedenceCnx (PLMIDSet)
	let PLMIDSetScopePrecedenceCnx (PLMIDSet)
	let PLMIDSetDRCnx (PLMIDSet)
	let PLMIDSetMaterializationCnx (PLMIDSet)
	let PLMIDSetScopePrequisiteCnx (PLMIDSet)
	let PLMIDSetValidPrequisiteCnx (PLMIDSet)
	let PLMIDSetProductSTL (PLMIDSet)
	let PLMIDSetCapableRsc (PLMIDSet)
	let PLMIDSetResourceRef (PLMIDSet)
	let PLMIDSetResourceStructure (PLMIDSet)
	let PLMIDSetSystemScope (PLMIDSet)
	let PLMIDSetRFLPCnx (PLMIDSet)
	let PLMIDSetValidRFLPCnx (PLMIDSet)
	let PLMIDSetImplementingOp  (PLMIDSet)
	let PLMIDSetDRPorts  (PLMIDSet)
	let PLMIDSetPortsAndCnx  (PLMIDSet)
	let PLMIDSetCapableCnx  (PLMIDSet)
	let PLMIDSetAlternateProcess  (PLMIDSet)
	let PLMIDSetReqCnx (PLMIDSet)
	let PLMIDSetReqs (PLMIDSet)
	let PLMIDSetReqSpec (PLMIDSet)
	let PLMIDSetReqSpecCompletude (PLMIDSet)
	let PLMIDSetReq (PLMIDSet)
	let PLMIDSetReqCompletude (PLMIDSet)
	let PLMIDSetReqChapter (PLMIDSet)
	let PLMIDSetReqChapterCompletude (PLMIDSet)
	let PLMIDSetSubstituteCnx (PLMIDSet)
	let PLMIDSetValidSubstituteCnx (PLMIDSet)
	let PLMIDSetResponsibilityCnx (PLMIDSet)
	let PLMIDSetSubstitute (PLMIDSet)
	let PLMIDSetResponsibility (PLMIDSet)
	let PLMIDSetHistoRefCnx (PLMIDSet)
	let PLMIDSetHistoRef (PLMIDSet)
	let PLMIDSetProductRef  (PLMIDSet)
	let PLMIDSetProductAggrRep (PLMIDSet)
	let PLMIDAllDoc (PLMIDSet)
	let PLMIDDocsAndScope (PLMIDSet)
	let PLMHowToTargetSet (PLMIDSet)

	PLMIDSetProcessRef = Restrict(iPLMIDSet, "DELFmiFunctionalModel/DELFmiFunctionReference") 
	PLMIDSetProcessInst = Restrict(iPLMIDSet,"DELFmiFunctionalModel/DELFmiFunctionInstance") 
	PLMIDSetProcess = PLMIDSetProcessRef + PLMIDSetProcessInst
		
	/* Export Alternate Processes */
	PLMRouteSetAlternate = ExecutePLMFunction("DELPPRContextModel", "ENOPcs_Process_addAlternateProcesses", PLMIDSetProcessRef)
	PLMIDSetAlternateProcess = Ids(PLMRouteSetAlternate,"all")
	
	/* Get HowTo Target from MBOM References*/
    PLMHowToTargetSet = ExecuteJavaProcedure("com.dassault_systemes.platform.model.mbom.procedures.ProcedureCalls_GetHowToTargets", PLMIDSetProcessRef)

	/* Retrieves all Ports and Cnx needed from reference         */
	PLMRouteSetPortsAndCnx  = ExecutePLMFunction("DELFmiFunctionalModel", "DELFmiFunctionalModel_addPortsAndCnxExceptImplCnx", PLMIDSetProcessRef)
	PLMIDSetPortsAndCnx = Ids(PLMRouteSetPortsAndCnx,"all")
	PLMIDSetDRPorts = Restrict(PLMIDSetPortsAndCnx, "DELFmiFunctionalModelPrerequisitePort/DELFmiProcessPrerequisitePort")
	PLMIDSetPrecedencePorts = Restrict(PLMIDSetPortsAndCnx, "DELFmiFunctionalModelPrecedencePort/DELFmiProcessPrecedencePort")
	PLMIDSetDRCnx = Restrict(PLMIDSetPortsAndCnx, "DELFmiFunctionalModelPrerequisiteCnx/DELFmiProcessPrerequisiteCnx") + Restrict(PLMIDSetPortsAndCnx, "DELFmiFunctionalModelPrerequisiteCnx1/DELFmiProcessPrerequisiteCnxCust")
	PLMIDSetPrecedenceCnx = Restrict(PLMIDSetPortsAndCnx, "DELFmiFunctionalModelPrecedenceCnx/DELFmiProcessPrecedenceCnx") + Restrict(PLMIDSetPortsAndCnx, "DELFmiFunctionalModelPrecedenceCnx1/DELFmiProcessPrecedenceCnxCust")
	PLMIDSetMaterializationCnx = Restrict(PLMIDSetPortsAndCnx, "DELFmiFunctionalModelPrereqMatCnx/DELFmiProcessPrereqMaterializationCnx")
	PLMIDSetCapableCnx = Restrict(PLMIDSetPortsAndCnx, "DELAsmAssemblyModelCnx/DELAsmProcessCanUseCnx")
	PLMIDSetReqCnx = Restrict(PLMIDSetPortsAndCnx,"PLMRequirementSpecifyHowToCnxAbstract/PLMReqSpecifyHowToCnxAbstract")
	PLMIDSetSubstituteCnx = Restrict(PLMIDSetPortsAndCnx, "DELFmiMfgSubstitute/DELFmiMfgSubstituteCnx")
	PLMIDSetResponsibilityCnx = Restrict(PLMIDSetPortsAndCnx, "DELMfgResponsibility/DELMfgResponsibilityCnx")
	PLMIDSetHistoRefCnx = Restrict(PLMIDSetPortsAndCnx, "PLMHistorizationCnx/PLMHistoLinkCnx")

	/* Get Original MBOM References from Historiztion connections */
	PLMRouteSetHistoRef = ExecutePLMFunction("PLMHistorizationCnx", "PLMHistorizationCnx_addHistoEntitiesFromCnx", PLMIDSetHistoRefCnx)
	PLMIDSetHistoRef = Ids(PLMRouteSetHistoRef,"all")

	/* Get Associated Requirements */
	PLMIDSetReqs = ExecuteJavaProcedure("com.dassault_systemes.platform.model.commonprocedures.procedures.ProcedureCalls_MBOMAndProcessHowToLinkTargetFromCnx", PLMIDSetReqCnx)


	/* Complete Requirement Specification*/
	PLMIDSetReqSpec = Restrict(PLMIDSetReqs, "Class/Requirement Specification")
	PLMIDSetReqSpecCompletude = ExecutePLMProcedure( "Rmt_ReqSpec_ExportCompletion", PLMIDSetReqSpec )

	/* Complete Requirement */
	PLMIDSetReq = Restrict(PLMIDSetReqs, "Class/Requirement")
	PLMIDSetReqCompletude = ExecutePLMProcedure( "Rmt_Requirement_ExportCompletion", PLMIDSetReq )

	/* Complete Requirement Chapter*/
	PLMIDSetReqChapter = Restrict(PLMIDSetReqs, "Class/Chapter")
	PLMIDSetReqChapterCompletude = ExecutePLMProcedure( "Rmt_Chapter_ExportCompletion", PLMIDSetReqChapter )

	/* Extract only valid Precedences Cnx */
	PLMIDSetScopePrecedenceCnx = PLMIDSetProcessInst + PLMIDSetPrecedencePorts 
	PLMIDSetValidPrecedenceCnx = ValidateSRs (PLMIDSetPrecedenceCnx,"DELFmi_PrecedenceCst_Target,DELFmi_PrecedenceCst_Source",PLMIDSetScopePrecedenceCnx)
	
	/* Extract only valid Prequisite Cnx */
	PLMIDSetScopePrequisiteCnx = PLMIDSetProcessInst + PLMIDSetDRPorts
	PLMIDSetValidPrequisiteCnx = ValidateSRs (PLMIDSetDRCnx,"DELFmi_PrerequisiteCst_Target,DELFmi_PrerequisiteCst_Source",PLMIDSetScopePrequisiteCnx)

	/* Retreives Capable Resources from Capable connexions*/
	PLMRouteSetCapableRsc = ExecutePLMFunction("DELPPRContextModel", "ENOPcs_Process_addCapableResourcesWithoutQueryFromCnx", PLMIDSetCapableCnx)
	PLMIDSetCapableRsc = Ids(PLMRouteSetCapableRsc,"all")

	/* complete documents with their versions */
	PLMIDAllDoc = Restrict(PLMIDSetCapableRsc, "Class/DOCUMENTS")
	PLMIDDocsAndScope = ExecutePLMProcedure( "DocumentCompletion", PLMIDAllDoc )
	
	/* Expand Resource structure (instance/reference only) */
	PLMIDSetResourceRef = Restrict(PLMIDSetCapableRsc, "PRODUCTCFG/VPMReference")
	PLMRouteSetResourceStructure = ExecutePLMFunction("DELPPRContextModel", "ENOPpr_PPRData_ExpandStructure", PLMIDSetResourceRef)
	PLMIDSetResourceStructure = Ids(PLMRouteSetResourceStructure,"all")

	/* Retrieves Product materialization pointed by DR Ports by PROCESSES */
	PLMRouteSetProductSTL = ExecutePLMFunction("DELPPRContextModel", "ENOPcx_PPRContext_addProductsLinkedToProcessPorts", PLMIDSetMaterializationCnx)
	PLMIDSetProductSTL = Ids(PLMRouteSetProductSTL,"all")

	/* Get aggregated representations from all Product/Resource references for coherence for 3DPart */
	PLMIDSetProductRef = Restrict(PLMIDSetResourceStructure,"PRODUCTCFG/VPMReference") + Restrict(PLMIDSetProductSTL,"PRODUCTCFG/VPMReference")
	PLMRouteSetProductAggrRep= ExecutePLMFunction( "PRODUCTCFG", "ProductCfg_Add3DPartRepresentation", PLMIDSetProductRef)
	PLMIDSetProductAggrRep = Ids(PLMRouteSetProductAggrRep,"all")

	/* Retrieves implementing System Scope */
	PLMRouteSetSystemScope = ExecutePLMFunction("DELFmiFunctionalModel", "DELFmiFunctionReference_SystemScopes", PLMIDSetProcessRef)
	PLMIDSetSystemScope = Restrict(Ids(PLMRouteSetSystemScope,"all"),"DELLmiProductionSystem/DELLmiProductionSystemReference")

	/* Get All implement connections under system  scope */
	PLMRouteSetRFLPCnx = ExecutePLMFunction("RFLPLMImplementConnection", "RFLPLMImplementConnection_AddAllImplementCnx", PLMIDSetSystemScope)
	PLMIDSetRFLPCnx = Restrict(Ids(PLMRouteSetRFLPCnx,"all"),"RFLPLMImplementConnection/RFLPLMImplementConnection")

	/* Extract only valid Implementing connections (scope or implement = pointing Process instance path or Process Ref) */
	PLMIDSetValidRFLPCnx = ValidateSRs (PLMIDSetRFLPCnx,"PLM_ImplementLink_Target",PLMIDSetProcess)

	/* Get impementing operations-system path */
	PLMRouteSetImplementingOp = ExecutePLMFunction("DELFmiFunctionalModel", "DELFmiFunctionReference_addImplementingOpFromRFLPCnx", PLMIDSetValidRFLPCnx)
	PLMIDSetImplementingOp = Ids(PLMRouteSetImplementingOp,"all")
	
	/* Extract only valid substitute connections  */
	PLMIDSetValidSubstituteCnx = ValidateSRs (PLMIDSetSubstituteCnx,"DELMfgSubstitute_Source",PLMIDSetProcessInst)

	/* Get pointed substitute from valid substitute connections  */
	PLMRouteSetSubstitute = ExecutePLMFunction("DELPPRContextModel", "ENOPcs_Process_addSubstituteFromCnx", PLMIDSetValidSubstituteCnx)
	PLMIDSetSubstitute = Ids(PLMRouteSetSubstitute,"all")

	/* Get Plant and Company from responsibility connections  */	
	PLMRouteSetResponsibility = ExecutePLMFunction("DELPPRContextModel", "ENOPpr_PPRData_addResponsibilityFromCnx", PLMIDSetResponsibilityCnx)
	PLMIDSetResponsibility = Ids(PLMRouteSetResponsibility,"all")


	oPLMIDSet = iPLMIDSet + PLMIDSetDRPorts + PLMIDSetPrecedencePorts + PLMIDSetMaterializationCnx + PLMIDSetCapableCnx + PLMIDSetValidPrecedenceCnx + PLMIDSetValidPrequisiteCnx + PLMIDSetCapableRsc + PLMIDSetResourceStructure + PLMIDSetProductSTL + PLMIDSetSystemScope + PLMIDSetValidRFLPCnx + PLMIDSetImplementingOp + PLMIDSetAlternateProcess + PLMIDSetReqCnx + PLMIDSetReqs + PLMIDSetReqSpecCompletude + PLMIDSetReqCompletude + PLMIDSetReqChapterCompletude + PLMIDSetResponsibilityCnx + PLMIDSetValidSubstituteCnx + PLMIDSetSubstitute + PLMIDSetResponsibility + PLMIDSetHistoRefCnx + PLMIDSetHistoRef + PLMIDSetProductAggrRep + PLMIDDocsAndScope + PLMHowToTargetSet
	/* Even if we don't mention precedence ports in the xml output, we need to export them for futur import */
}
