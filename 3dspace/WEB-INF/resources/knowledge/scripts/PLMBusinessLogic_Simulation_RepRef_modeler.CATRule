Let Prefix=""
Let incAttr=""
Let Operation=""
Let CopyFrom(SIMObjSimulationRepReferenceGeneric)
Let CopyFromId=""
Let BufferStart=""
Let BufferEnd=""
Let AttrSize=0
Let Reference(PLMEntity)
Let New_PLMExternalID=""

Operation=Parameters->GetAttributeString("OperationId")

if (Parameters->HasAttribute("IncSessionId") == true) {
      incAttr = ToString(Parameters->GetAttributeInteger("IncSessionId"))
}

if (Operation=="ImportAsNew" )
{
	/* ImportAsNew */
	if (Parameters->HasAttribute("IdCloningString") == true)
	{
		Prefix = Parameters->GetAttributeString("IdCloningString")
	}
	if (Prefix == "" ) 
	{
	  Prefix = Parameters->GetAttributeString("IdString")
	}
	if ( Parameters->HasAttribute("CopyFrom") == true)
	{
	 Set CopyFrom = Parameters->GetAttributeObject("CopyFrom")
	 CopyFromId = CopyFrom->GetAttributeString("PLM_ExternalID") 
	}
	New_PLMExternalID  =  Prefix + CopyFromId 

	ThisObject.V_description = "Simulation imported as New"
}
else if ( Operation=="Cloning" )
{
	/* cloning */
	Prefix = ""
	if (Parameters->HasAttribute("IdCloningString") == true)
	{
	 Prefix = Parameters->GetAttributeString("IdCloningString")
	}
	   
	if ( Parameters->HasAttribute("CopyFrom") == true)
	{
	 Set CopyFrom = Parameters->GetAttributeObject("CopyFrom")
	 CopyFromId = CopyFrom->GetAttributeString("PLM_ExternalID") 
	}
	if ( Prefix <> "" )
	{
	  New_PLMExternalID  =  Prefix  + CopyFromId + "-" +incAttr
	}
	else
	{
	  New_PLMExternalID  =   "Clone-"  + CopyFromId + "-"+ incAttr
	}

	ThisObject.V_description = "Simulation cloned"
}

else
{
  /* new */ 
  if (Parameters->HasAttribute("IdString") == true)
  {
      Prefix = Parameters->GetAttributeString("IdString")
      if (Prefix == NULL) Prefix = ""
  }

  if (Parameters->HasAttribute("CoupledRef") == true)
  {
    Set Reference = Parameters ->GetAttributeObject("CoupledRef")  
  } 


  if (Parameters->HasAttribute("IncSessionId") == true) 
  {
      if ( Reference == NULL ) 
      {
       /* Adding Unique Server Key to Assuming PLM_ExternalID is part of ThisObject 'Identifier Set' */
       New_PLMExternalID = Prefix +  "-Behavior" + GetUniqueKey( Parameters->GetAttributeString("CustoType"))
       /* ThisObject.PLM_ExternalID = Prefix +  "-Behavior" + ToString(Parameters->GetAttributeInteger("IncSessionId"))*/
      }
      else 
      {
       /* Adding Unique Server Key to Assuming PLM_ExternalID is part of ThisObject 'Identifier Set' */
       New_PLMExternalID = Reference.PLM_ExternalID +  "-Behavior" + GetUniqueKey( Parameters->GetAttributeString("CustoType"))
       /* ThisObject.PLM_ExternalID = Reference.PLM_ExternalID +  "-Behavior" + ToString(Parameters->GetAttributeInteger("IncSessionId"))*/
      }
      ThisObject.V_description = "RepRef behavior created by " + Parameters.UserID
  }
}



AttrSize=  New_PLMExternalID.Length() 
if ( AttrSize> 60 )
{
  BufferStart = New_PLMExternalID.Extract(0,8) 
  BufferEnd = New_PLMExternalID.Extract( AttrSize- 49, 49 )
  New_PLMExternalID = BufferStart + "..." + BufferEnd
}
 
if ( New_PLMExternalID <> "" )
{
	ThisObject.PLM_ExternalID  = New_PLMExternalID
}
